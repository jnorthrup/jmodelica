rem @echo off

rem Batch procedure to compile and link the model for Dymosim.
rem Uses Microsoft Visual C/C++ version 6.0 (and later).
rem
rem Version 1.5, 2003-11-25

if not "%1" == "-d" goto haveDYMOLA
set DYMOLA=%2
shift
shift

:haveDYMOLA

if not "%1" == "-vc" goto haveVC
set MSVCDir=%2
shift
shift

:haveVC

if not "%1" == "-l" goto haveD2
set EXTRALIB=%EXTRALIB% %2.lib
shift
shift
goto haveDYMOLA
:haveD2

echo. > dslog.txt
echo Compiling and linking the model (Visual C++). >> dslog.txt
echo. >> dslog.txt

if not exist dymosim.exe goto init
del dymosim.exe > NUL

:init
if "%MSVCDir%" == "" call "%DYMOLA%\bin\setvcdir.bat"
if exist "%MSVCDir%\vcvars32.bat" goto compile
if exist "%MSVCDir%\bin\vcvars32.bat" goto compile
echo. >> dslog.txt
echo Environment variable MSVCDir does not point to the VC++ installation directory. >> dslog.txt
echo MSVCDir is %MSVCDir%. >> dslog.txt
echo. >> dslog.txt
goto error


:compile
rem fix
copy dsmodel.c dsmodel.tmp
echo #include "windows.h" > dsmodel.c
echo. >> dsmodel.c
type dsmodel.tmp >> dsmodel.c

if exist "%MSVCDir%\vcvars32.bat" call "%MSVCDir%\vcvars32" x86 >> dslog.txt
if exist "%MSVCDir%\bin\vcvars32.bat" call "%MSVCDir%\bin\vcvars32" x86 >> dslog.txt
set LIB=%DYMOLA%\bin\lib;%LIB%
set INCLUDE=.;%DYMOLA%\source;%INCLUDE%
set CL=/nologo /W3 /D"WIN32" /D"_CONSOLE" /D"DYMOSIM" /Oy /Ob1 /Oi /MD /D"_CRT_SECURE_NO_DEPRECATE"
set LINK=/stack:0x500000 /NODEFAULTLIB:LIBCD /NODEFAULTLIB:LIBC /NODEFAULTLIB:LIBCMT /OPT:NOREF user32.lib advapi32.lib comdlg32.lib wsock32.lib comctl32.lib netapi32.lib libds.lib dymosim.obj %EXTRALIB%
if not exist %1ext*.c goto simple
cl /Od /Fe"dymosim.exe" %1.c %1ext*.c %2 %3 %4 %5 >> dslog.txt 2>>&1
if exist %1ext*.obj del %1ext*.obj > NUL
goto doneComp
:simple
cl /Od /Fe"dymosim.exe" %1.c %2 %3 %4 %5 >> dslog.txt 2>>&1
goto doneComp
:doneComp
del %1.obj > NUL
if not exist dymosim.exe goto error

if not exist dymosim.exe.manifest goto noManifest
mt -manifest dymosim.exe.manifest -outputresource:dymosim.exe;#1
del dymosim.exe.manifest
:noManifest

goto Finished

:error
echo. >> dslog.txt
echo Error generating Dymosim. >> dslog.txt
echo. >> dslog.txt
echo Failed > stat.
goto exit

:Finished
echo. >> dslog.txt
echo Successful generation of Dymosim. >> dslog.txt
echo. >> dslog.txt
echo Finished > stat.

:exit
copy dslog.txt buildlog.txt > NUL
ren stat. status. > NUL
